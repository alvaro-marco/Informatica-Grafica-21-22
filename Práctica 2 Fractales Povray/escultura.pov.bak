/* --------------------------
Entry: 0024
WWW: http://www.b0n541.net
Title: Julia Gold Nugget
Author: Bonsai
---------------------------*/

// Julia Gold Nugget

//#version 3.5;  
/*-------------------------------------------------------------------------    
global_settings {
	max_trace_level 25
	ambient_light 0
	assumed_gamma 1
}    
background {
	
	color rgb 0.25
}//-------------------------------------------------------------------------    
camera {
	location <0, 1, -3>
	look_at <0, 0, 0>          
	right x*image_width/image_height
}                
light_source {
	<-15, 15, -15>
	color rgb 1
    area_light
    <4, 0, 4>, <0, 4, 4>, 10, 10
    adaptive 1 
	jitter
    circular
}
light_source {
	<0, 5, 5>
	color rgb 0.5
	
	shadowless
}
light_source {
	<5, 0, 0>
	color rgb 0.5
	shadowless
}
//-------------------------------------------------------------------------      
difference {
	cylinder {
		
		<-100, 20, 0>,
		< 100, 20, 0>, 21
	}
	
	cylinder {
		
		<-101, 20, 0>,
		< 101, 20, 0>, 20
	}
	
	box {
		
		<-101, -2, -22>,
		< 101, 42,   0>
	}
	
	translate <0, -1, 10>
	
	material {
		
		texture {
			
			pigment {
				
				color rgb 1
			}
		}
	}
}    
plane {
	
	<0, 1, 0>, -1
	
	material {
		
		texture {
			
			pigment {
				
				color rgb 1
			}
		}
	}
}     
//------------------------------------------------------------------------- */  
#declare iteration = 3;     

#declare base = object{
    union {
        
        #while (iteration < 7)
            
            difference {
                
                julia_fractal {
                    
                    <-0.083, 0.0, -0.83, -0.025>
                    quaternion
                    sqr
                    max_iteration iteration
                    precision 10
                    rotate <0, 0, 45>
                }
                
                box {
                    
                    <-4, 0.5 - 2/iteration, -4>, <4, 2, 4>
                }
                material {
            
                    // Copied from stones1.inc
                    // T_Grnt15		
                    texture {
                        
                        pigment {
                            
                            granite
                            turbulence 0.6
                            
                            #declare brighten = 0.2*(iteration - 3);
                            
                            color_map {
                                
                                [0.000, 0.104   color rgbt <0.161, 0.133, 0.118, 0.000> + brighten
                                                color rgbt <0.110, 0.082, 0.071, 0.000>]
                                [0.104, 0.252   color rgbt <0.110, 0.082, 0.071, 0.000> 
                                                color rgbt <0.161, 0.133, 0.118, 0.000> + brighten]
                                [0.252, 0.383   color rgbt <0.161, 0.133, 0.118, 0.000> + brighten 
                                                color rgbt <0.000, 0.000, 0.000, 0.000>]
                                [0.383, 0.643   color rgbt <0.000, 0.000, 0.000, 0.000> 
                                                color rgbt <0.161, 0.133, 0.118, 0.000> + brighten]
                                [0.643, 0.783   color rgbt <0.161, 0.133, 0.118, 0.000> + brighten
                                                color rgbt <0.220, 0.149, 0.137, 0.000> + brighten]
                                [0.783, 0.922   color rgbt <0.220, 0.149, 0.137, 0.000> + brighten
                                                color rgbt <0.000, 0.000, 0.000, 0.000>]
                                [0.922, 0.983   color rgbt <0.000, 0.000, 0.000, 0.000> 
                                                color rgbt <0.220, 0.149, 0.137, 0.000> + brighten]
                                [0.983, 1.001   color rgbt <0.220, 0.149, 0.137, 0.000> + brighten
                                                color rgbt <0.161, 0.133, 0.118, 0.000> + brighten]
                            }
                        }
                        
                        scale 0.2
                    }
                }
            }
            #declare iteration = iteration + 1;
            
        #end                        
    }
} 
#declare rosco = object {
    julia_fractal {
        <-0.083, 0.0, -0.83, -0.025>
        quaternion
        sqr
        max_iteration 9
        precision 50
        
        rotate <0, 0, 45>
        
        material {
            
            texture {
                
                // Copied from golds.inc
                // T_Gold_1A
                #declare GoldBase = <1.00, 0.875, 0.575>;
                #declare CVect1 = GoldBase  - <0.00, 0.20, 0.40>;
                #declare P_Gold1 = rgb CVect1;
                #declare R_GoldA =  P_Gold1 * 0.30 + 0.25;
                #declare A_GoldA =  P_Gold1 * 0.12 + 0.1;
                #declare D_GoldA    = 1-(((R_GoldA.red+R_GoldA.green+R_GoldA.blue)/3)
                                        + ((A_GoldA.red+A_GoldA.green+A_GoldA.blue)/3));
                
                #declare D_GoldA = max(D_GoldA, 0);
                
                #declare F_MetalA  = finish {
                    brilliance 2
                    diffuse D_GoldA
                    ambient A_GoldA
                    reflection R_GoldA
                    metallic 1
                    specular 0.20
                    roughness 1/20
                }			
                pigment { P_Gold1 } 
                finish { F_MetalA }
            }
        }
    }
} 

//-----------------------------------------------------------------------------------------------------------------------   
/* --------------------------
Entry: 0023
WWW: 
Title: Pernelle
Author: Noe Falzon

Spheres in spheres in spheres... This cristalline and simple fractalicity (fractalism, fractalicism... ;) ) is named after my sister's name whose birthday occurs today.
There is a simple use of a recursive macro, who calls itself a number of time specified by a variable.
The more you give a high value to max_trace_level, the more you'll get a light image, with a thin dark ray in the middle, along the little spheres.
Each sphere is transparent and reflective, which gives this aspect to the whole object.
There is a white plane beneath, to help the transparency effect.
---------------------------*/

// Persistence of Vision Ray Tracer Scene Description File
// File: fractal.pov
// Vers: 3.5
// Desc: For the Bourke Fractal Contest
// Date: 16/04/2004 pour le rendu final

global_settings {
  assumed_gamma 1.0
  max_trace_level 10
}
/* ----------------------------------------
camera {
  location  <0.4, 0.4, -0.2>
  right     x*image_width/image_height
  look_at   <0.0, 0.0,  0.0>
}
sky_sphere {
  pigment {
    gradient y
    color_map {
      [0.0 rgb <0.6,0.7,1.0>]
      [0.7 rgb <0.0,0.1,0.8>]
    }
  }
}
light_source {
  <0, 0, 0>
  color rgb <1, 1, 1>
  translate <-30, 30, -30>
}
// ---------------------------------------- */
plane {
  y, -1
  pigment { color rgb 1 }
}
#macro maSphere(zPos, zIter, zRad)
 sphere { zPos, zRad }
 
 #if (zIter > 0) 
  maSphere ( zPos+0.5*zRad*x, zIter-1 zRad*0.5 )
  maSphere ( zPos-0.5*zRad*x, zIter-1 zRad*0.5 )
 #end
#end
union { maSphere(0,7,1) texture {pigment {color rgbft <0.8,0.8,1,0.9,0.1>} finish {reflection 0.3}}}      


//-----------------------------------------------------------------------------------------------------------------------   
#declare escultura1 = object{
    union{
        object{base}
        object {rosco}
    }
}

//object {escultura2}
